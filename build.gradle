plugins {
    id 'java'
    id 'application'
    id 'io.freefair.lombok' version '5.3.0'
}

group 'net.undeadborn'
version '1.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
}

repositories {
    mavenCentral()
}

task createGSD2Unpacker(type: CreateStartScripts) {
    applicationName = 'gsd2unpacker'
    mainClassName = 'net.undeadborn.suikoden.tools.GSD2Unpacker'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

task createGSD2Repacker(type: CreateStartScripts) {
    applicationName = 'gsd2repacker'
    mainClassName = 'net.undeadborn.suikoden.tools.GSD2Repacker'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createGSD2Unpacker)
    from(createGSD2Repacker)
    fileMode = 0755
}